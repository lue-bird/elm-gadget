module Main exposing (..)

import IR.Diff
import IR.Fuzz
import IR.Json
import IR.Html
import IR.Random
import Fuzz
import Html
import IR
import Json.Decode as JD
import Json.Encode as JE
import Random


type Example
    = Yellow
    | Green String Record
    | Red Char (List Bool)


type alias Record =
    { field1 : String
    , field2 : Int
    }


recordCodec : IR.Codec Record Record
recordCodec =
    IR.succeed Record
        |> IR.andMap .field1 IR.string
        |> IR.andMap .field2 IR.int


exampleCodec : IR.Codec Example Example
exampleCodec =
    IR.custom
        (\red yellow green value ->
            case value of
                Red b s ->
                    red b s

                Yellow ->
                    yellow

                Green s r ->
                    green s r
        )
        |> IR.variant2 Red IR.char (IR.list IR.bool)
        |> IR.variant0 Yellow
        |> IR.variant2 Green IR.string recordCodec
        |> IR.endCustom


main : Html.Html msg
main =
    let
        codec = 
            IR.list exampleCodec

        old = 
            Random.step (IR.Random.generator codec) (Random.initialSeed 14)
                |> Tuple.first

        new =
            Random.step (IR.Random.generator codec) (Random.initialSeed 16)
                |> Tuple.first

        diff =
            IR.Diff.diff codec old new

        patched =
            IR.Diff.patch codec diff old

        fuzzed =
            Fuzz.examples 1 (IR.Fuzz.fuzzer codec)

        encoded =
            JE.encode 2 (IR.Json.encode codec old)

        decoded =
            JD.decodeString (IR.Json.decoder codec) encoded
    in
    Html.div []
        [ head "Generator ('old' value)"
        , show old
        , IR.Html.view codec old
        , show (IR.irType codec)
        , head "Generator ('new' value)"
        , show new
        , IR.Html.view codec new
        , head "Diff between 'old' & 'new'"
        , show diff
        , head "Patch 'old' with diff"
        , show patched
        , head "Did patch work?"
        , show (patched == Ok new)
        , head "JSON encoder (old value)"
        , Html.pre [] [Html.text encoded]
        , head "JSON decoder (old value)"
        , show decoded
        , head "Fuzzer"
        , show fuzzed
        ]


head : String -> Html.Html msg
head txt =
    Html.h3 [] [ Html.text txt ]


show : a -> Html.Html msg
show a =
    Html.pre [] [ Html.text (Debug.toString a) ]
