module Gadget.Html exposing (..)

import Gadget.IR as IR
import Html as H
import Html.Attributes as HA
import Set


view : IR.Gadget a -> a -> H.Html msg
view codec value =
    IR.fromInput codec value
        |> htmlAdapter
        |> List.singleton
        |> H.article [ HA.class "elm-gadget" ]


primitive : H.Html msg -> (String -> H.Html msg) -> String -> String -> H.Html msg
primitive quoteHtml valueWrapper typeName value =
    H.dl []
        [ H.div [ HA.class "primitive", HA.class typeName ]
            [ H.dt [] [ H.em [] [ H.text typeName ] ]
            , H.dd [] [ H.span [] [ quoteHtml, valueWrapper value, quoteHtml ] ]
            ]
        ]


labelled : String -> H.Html msg -> H.Html msg
labelled label inner =
    H.dl []
        [ H.div [ HA.class "labelled" ]
            [ H.dt []
                [ H.em [] [ H.text "Label" ]
                , H.code [] [ H.text label ]
                ]
            , H.dd [] [ inner ]
            ]
        ]


quotedPrimitive : String -> String -> String -> H.Html msg
quotedPrimitive quote =
    primitive (H.span [ HA.class "quote" ] [ H.text quote ]) (\value -> H.code [] [ H.text value ])


unquotedPrimitive : String -> String -> H.Html msg
unquotedPrimitive =
    primitive (H.text "") (\value -> H.code [] [ H.text value ])


combinator : String -> String -> List IR.IR -> H.Html msg
combinator typeName meta items =
    if List.isEmpty items then
        H.div [ HA.class "combinator", HA.class typeName ]
            [ H.summary []
                [ H.strong [] [ H.text typeName ]
                , H.text (" " ++ meta)
                ]
            ]

    else
        H.details [ HA.class "combinator", HA.class typeName ]
            [ H.summary []
                [ H.strong [] [ H.text typeName ]
                , H.text (" " ++ meta)
                ]
            , H.ol
                []
                (List.map (\item -> H.li [] [ htmlAdapter item ]) items)
            ]


htmlAdapter : IR.IR -> H.Html msg
htmlAdapter irValue =
    case irValue of
        IR.Labelled labels inner ->
            labelled (Set.toList labels |> String.join ", ") (htmlAdapter inner)

        IR.Bool b ->
            unquotedPrimitive "Bool"
                (if b then
                    "True"

                 else
                    "False"
                )

        IR.Char c ->
            quotedPrimitive "'" "Char" (String.fromChar c)

        IR.String s ->
            quotedPrimitive "\"" "String" s

        IR.Int i ->
            unquotedPrimitive "Int" (String.fromInt i)

        IR.Float f ->
            unquotedPrimitive "Float" (String.fromFloat f)

        IR.Custom selected variant ->
            let
                args =
                    case variant of
                        IR.Variant0 ->
                            []

                        IR.Variant1 arg ->
                            [ arg ]

                        IR.Variant2 arg1 arg2 ->
                            [ arg1
                            , arg2
                            ]

                        IR.Variant3 arg1 arg2 arg3 ->
                            [ arg1
                            , arg2
                            , arg3
                            ]

                        IR.Variant4 arg1 arg2 arg3 arg4 ->
                            [ arg1
                            , arg2
                            , arg3
                            , arg4
                            ]

                        IR.Variant5 arg1 arg2 arg3 arg4 arg5 ->
                            [ arg1
                            , arg2
                            , arg3
                            , arg4
                            , arg5
                            ]

                numArgs =
                    List.length args
            in
            combinator
                "Custom"
                ("variant #"
                    ++ String.fromInt selected
                    ++ " with "
                    ++ String.fromInt numArgs
                    ++ " argument"
                    ++ (if numArgs == 1 then
                            ""

                        else
                            "s"
                       )
                )
                args

        IR.Product fields ->
            let
                count =
                    List.length fields
            in
            combinator
                "Product"
                ("with "
                    ++ String.fromInt count
                    ++ " field"
                    ++ (if count == 1 then
                            ""

                        else
                            "s"
                       )
                )
                fields

        IR.List items ->
            let
                count =
                    List.length items
            in
            combinator
                "List"
                ("with "
                    ++ String.fromInt count
                    ++ " item"
                    ++ (if count == 1 then
                            ""

                        else
                            "s"
                       )
                )
                items
